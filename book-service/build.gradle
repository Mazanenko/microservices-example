buildscript {
	ext {
		mapstruct = '1.5.3.Final'
		openapi = '1.6.13'
	}
}

plugins {
	id 'java'
	id 'org.springframework.boot' version '3.0.4'
	id 'io.spring.dependency-management' version '1.1.0'
	id "org.openapi.generator" version "6.4.0"
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
//    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.liquibase:liquibase-core:4.20.0'
	implementation 'io.swagger.core.v3:swagger-annotations:2.2.8'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.3'
	implementation "org.mapstruct:mapstruct:${mapstruct}"

	compileOnly 'org.projectlombok:lombok'

	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	runtimeOnly 'org.postgresql:postgresql'

	annotationProcessor 'org.projectlombok:lombok'
	annotationProcessor "org.mapstruct:mapstruct-processor:${mapstruct}"
	annotationProcessor "org.projectlombok:lombok-mapstruct-binding:0.2.0"

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
}

tasks.named('test') {
	useJUnitPlatform()
}

openApiGenerate {
	generatorName = "spring"
	inputSpec = "$projectDir/src/main/resources/openapi/specification.yaml".toString()
	outputDir = "$buildDir/generated/openapi".toString()
	apiPackage = "com.example.resourceserver.openapi.api"
	modelPackage = "com.example.resourceserver.openapi.model"
	configOptions =  [
			dateLibrary: "java8",
			useSpringBoot3: "true",
			generateApis: "true",
			generateApiTests: "false",
			generateModels: "true",
			generateModelTests: "false",
			generateModelDocumentation: "false",
			generateSupportingFiles: "false",
			hideGenerationTimestamp: "true",
			interfaceOnly: "true",
			library: "spring-boot",
			serializableModel: "true",
			useBeanValidation: "true",
			useTags: "true",
			implicitHeaders: "true",
			openApiNullable: "false",
			oas3: "true"
	]
	typeMappings = [
			OffsetDateTime: 'java.time.LocalDateTime'
	]
}

sourceSets {
	main {
		java {
			srcDirs("$buildDir/generated/openapi/src/main/java")
		}
	}
}


compileJava {
	dependsOn  tasks.openApiGenerate
}
